
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьРеквизитыФормы();
	ВывестиОтчеты();
		
КонецПроцедуры  

#КонецОбласти       

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоследниеИзмененияОтображатьКоличествоПриИзменении(Элемент)
	ПоследниеИзмененияОтображатьКоличествоПриИзмененииНаСервере();
КонецПроцедуры   

&НаКлиенте
Процедура НовыеИнтеграцииОтображатьКоличествоПриИзменении(Элемент)
	НовыеИнтеграцииОтображатьКоличествоПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьКоличествоСистемПриИзменении(Элемент)
	ВыводитьКоличествоСистемПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОтборТехнологияИнтеграциииПриИзменении(Элемент)
	ОтборТехнологияИнтеграциииПриИзмененииНаСервере();
КонецПроцедуры

#Область ОбработчикиРасшифровки

&НаКлиенте
Процедура ТабДокПоследниеИзмененныеИнтеграцииОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)

	СтандартнаяОбработка = Ложь; 

	ЗначениеИЗРасшифровки = ЗначениеИЗРасшифровки(Расшифровка, ДанныеРасшифровкиПоследниеИзмененныеИнтеграцииСтрока);
	Если ЗначениеЗаполнено(ЗначениеИЗРасшифровки) Тогда
		ПоказатьЗначение(Неопределено, ЗначениеИЗРасшифровки);  
	КонецЕсли;

КонецПроцедуры  

&НаКлиенте
Процедура ТабДокНовыеИнтеграцииОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)

	СтандартнаяОбработка = Ложь; 

	ЗначениеИЗРасшифровки = ЗначениеИЗРасшифровки(Расшифровка, ДанныеРасшифровкиНовыеИнтеграцииСтрока);
	Если ЗначениеЗаполнено(ЗначениеИЗРасшифровки) Тогда
		ПоказатьЗначение(Неопределено, ЗначениеИЗРасшифровки);  
	КонецЕсли;

КонецПроцедуры

#КонецОбласти 

#КонецОбласти     

#Область СлужебныеПроцедурыИФункции    

#Область РаботаСреквизитамиФормы

&НаСервере
Процедура ИнициализироватьРеквизитыФормы()
	
	ПоследниеИзмененияОтображатьКоличество = 5;
	НовыеИнтеграцииОтображатьКоличество = 5;
	ВыводитьКоличествоСистем = 10;
	
КонецПроцедуры

#КонецОбласти

#Область ВыводОтчетов

&НаСервере
Процедура ВывестиОтчеты()
	
	ВывестиОтчетАнализРаботыПользователейСИнтеграциями(); 
	ВывестиОтчетНовыеИнтеграции(); 
	ВывестиОтчетАнализИнтеграцийПоКоличеству();

КонецПроцедуры    

&НаСервере
Процедура ВывестиОтчетАнализИнтеграцийПоКоличеству()
	
	ТабДокАнализИнтеграцийПоКоличеству.Очистить();
	
	Отчет = Отчеты.АнализИнтеграцийПоКоличеству.Создать();
	Отчет.УстановитьТехнологиюИнтеграции(ОтборТехнологияИнтеграциии);
	Отчет.УстановитьКоличествоИС(ВыводитьКоличествоСистем);     
	Отчет.УстановитьИмяВыводимогоВарианта("РабочийСтолДиаграммаКоличествоПоИС");
	Отчет.СкомпоноватьРезультат(ТабДокАнализИнтеграцийПоКоличеству);  
	
КонецПроцедуры 

&НаСервере
Процедура ВывестиОтчетАнализРаботыПользователейСИнтеграциями()
	
	Перем ДанныеРасшифровки; 
	
	ТабДокПоследниеИзмененныеИнтеграции.Очистить(); 
	
	Отчет = Отчеты.АнализРаботыПользователейСИнтеграциями.Создать();
	Отчет.УстановитьИмяВыводимогоВарианта("РабочийСтолПоследниеИзмененияПользователями");
	Отчет.УстановитьКоличествоВыводимыхИнтеграций(ПоследниеИзмененияОтображатьКоличество);
	Отчет.СкомпоноватьРезультат(ТабДокПоследниеИзмененныеИнтеграции, ДанныеРасшифровки);
	
	ДанныеРасшифровкиПоследниеИзмененныеИнтеграцииСтрока = ПоместитьВоВременноеХранилище(ДанныеРасшифровки, УникальныйИдентификатор);
	
КонецПроцедуры 

&НаСервере
Процедура ВывестиОтчетНовыеИнтеграции()
	
	Перем ДанныеРасшифровки;
	
	ТабДокНовыеИнтеграции.Очистить();
	
	Отчет = Отчеты.АнализРаботыПользователейСИнтеграциями.Создать();
	Отчет.УстановитьИмяВыводимогоВарианта("РабочийСтолНовыеИнтеграции");
	Отчет.УстановитьКоличествоВыводимыхИнтеграций(НовыеИнтеграцииОтображатьКоличество);
	Отчет.СкомпоноватьРезультат(ТабДокНовыеИнтеграции, ДанныеРасшифровки);   

	ДанныеРасшифровкиНовыеИнтеграцииСтрока = ПоместитьВоВременноеХранилище(ДанныеРасшифровки, УникальныйИдентификатор);
	              
КонецПроцедуры
			  
#КонецОбласти

&НаСервере
Процедура ПоследниеИзмененияОтображатьКоличествоПриИзмененииНаСервере()
	ВывестиОтчетАнализРаботыПользователейСИнтеграциями();
КонецПроцедуры

&НаСервере
Процедура НовыеИнтеграцииОтображатьКоличествоПриИзмененииНаСервере()
	ВывестиОтчетНовыеИнтеграции()
КонецПроцедуры

&НаСервере
Процедура ВыводитьКоличествоСистемПриИзмененииНаСервере()
	ВывестиОтчетАнализИнтеграцийПоКоличеству();
КонецПроцедуры  

&НаСервере
Процедура ОтборТехнологияИнтеграциииПриИзмененииНаСервере()
	ВывестиОтчетАнализИнтеграцийПоКоличеству();
КонецПроцедуры

&НаСервере
Функция ЗначениеИЗРасшифровки(Расшифровка, АдресРасшифровки)

	ДанныеРасшифровки = ПолучитьИзВременногоХранилища(АдресРасшифровки); 	
	Если ТипЗнч(Расшифровка) = Тип("ИдентификаторРасшифровкиКомпоновкиДанных") тогда 
		ЭлементРасшифровки = ДанныеРасшифровки.Элементы[Расшифровка]; 
		Если ТипЗнч(ЭлементРасшифровки) = Тип("ЭлементРасшифровкиКомпоновкиДанныхПоля") Тогда 
			Для Каждого Поле Из ЭлементРасшифровки.ПолучитьПоля() Цикл 
				Возврат Поле.Значение; 
			КонецЦикла; 
		КонецЕсли; 
	КонецЕсли; 

КонецФункции

#КонецОбласти

